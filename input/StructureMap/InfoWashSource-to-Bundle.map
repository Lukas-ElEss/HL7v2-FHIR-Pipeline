map "http://hsrt-kkrt.org/fhir/StructureMap/InfoWashSource-to-Bundle" = "InfoWashSourceToBundle"

// ---------- Uses: Source Logical Model + Substrukturen ----------
uses "http://hsrt-kkrt.org/fhir/StructureDefinition/InfoWashSource" alias InfoWashSource as source

// ---------- Uses: Targets ----------
uses "http://hl7.org/fhir/StructureDefinition/Bundle"       alias Bundle        as target
uses "http://hl7.org/fhir/StructureDefinition/Patient"      alias Patient       as produced
uses "http://hl7.org/fhir/StructureDefinition/Condition"    alias Condition     as produced
uses "http://hl7.org/fhir/StructureDefinition/ServiceRequest" alias ServiceRequest as produced
uses "http://hl7.org/fhir/StructureDefinition/Provenance"   alias Provenance    as produced

// ---------- Hauptgruppe: Startet alles ----------
group IWS2Bundle(source src : InfoWashSource, target bundle : Bundle) {
  // Bundle-Metadaten
  src -> bundle.id = uuid() "bundleid";
  src -> bundle.type = 'transaction' "bundletype";

  // Sequenzieller Aufruf: PID → Patient → DG1 → Condition → ServiceRequest → Provenance
  src then PIDtoPatient(src, bundle) "patient";
}

// ========================== Sequenzielle Gruppen ==========================

// ----- PID → Patient (Startet die Sequenz) -----
group PIDtoPatient(source src : InfoWashSource, target bundle : Bundle) {
  // Patient erstellen
  src.PIDSegment as pid
    -> bundle.entry as ePat,
       ePat.resource = create('Patient') as patient,
       patient.id = uuid() as patUuid,
       ePat.fullUrl = append('urn:uuid:', patUuid) as patFull
    then { 
      pid then PIDtoPatientFields(pid, patient) "patient-fields";
      // Nächste Gruppe aufrufen: DG1 → Condition
      src then DG1toCondition(src, bundle, patient) "condition";
    } "patient-entry";
}

// ----- DG1 → Condition (Nach Patient) -----
group DG1toCondition(source src : InfoWashSource, target bundle : Bundle, target patient : Patient) {
  // Condition erstellen
  src.DG1Segment as dg1
    -> bundle.entry as eCond,
       eCond.resource = create('Condition') as condition,
       condition.id = uuid() as condUuid,
       eCond.fullUrl = append('urn:uuid:', condUuid) as condFull
    then { 
      dg1 then DG1toConditionFields(dg1, condition, patient) "condition-fields";
      // Nächste Gruppe aufrufen: ServiceRequest
      src then ServiceRequestProcessing(src, bundle, patient, condition) "servicerequest";
    } "condition-entry";
}

// ----- ServiceRequest Processing (Nach Condition) -----
group ServiceRequestProcessing(source src : InfoWashSource, target bundle : Bundle, target patient : Patient, target condition : Condition) {
  // ServiceRequest erstellen
  src -> bundle.entry as eSR,
       eSR.resource = create('ServiceRequest') as sr,
       sr.id = uuid() as srUuid,
       eSR.fullUrl = append('urn:uuid:', srUuid) as srFull
  then {
    // ServiceRequest befüllen
    src then ServiceRequestFields(src, sr, patient, condition) "servicerequest-fields";
    // Nächste Gruppe aufrufen: Provenance
    src then ProvenanceProcessing(src, bundle, patient, condition, sr) "provenance";
  } "servicerequest-entry";
}

// ----- Provenance Processing (Letzte Gruppe) -----
group ProvenanceProcessing(source src : InfoWashSource, target bundle : Bundle, target patient : Patient, target condition : Condition, target sr : ServiceRequest) {
  // Provenance erstellen
  src.CTXSegment as ctx
    -> bundle.entry as eProv,
       eProv.resource = create('Provenance') as prov,
       prov.id = uuid() as provUuid,
       eProv.fullUrl = append('urn:uuid:', provUuid)
  then {
    src then ProvenanceFields(src, prov, patient, condition, sr) "provenance-fields";
  } "provenance-entry";
}

// ========================== Feld-Befüllungs-Gruppen ==========================

// ----- PID → Patient Fields -----
group PIDtoPatientFields(source pid : PIDSegment, target patient : Patient) {
  // Identifier (PID-3)
  pid -> patient.identifier = create('Identifier') as id then {
    pid.PID_3_1 as v -> id.value = v "PID_3_1";
    pid.PID_3_4_1 as sys -> id.system = sys "PID_3_4_1";
    pid.PID_3_5 as tcode
      -> id.type as t, t.coding as c, c.system as system, c.code as code then {
            pid -> code.value = translate(tcode, 'http://hl7.org/fhir/uv/v2mappings/ConceptMap/table-hl70203-to-v2-0203', 'code') "code";
            pid -> system.value = 'http://terminology.hl7.org/CodeSystem/v2-0203' "system";
         } "PID_3_5";
  } "identifier";

  // Name (PID-5)
  pid -> patient.name = create('HumanName') as nm then {
    pid.PID_5_1_1 as fam -> nm.family = fam "PID_5_1_1";
    pid.PID_5_2   as g1  -> nm.given  = g1  "PID_5_2";
    pid.PID_5_3   as g2  -> nm.given  = g2  "PID_5_3";
    pid.PID_5_4   as sfx -> nm.suffix = sfx "PID_5_4";
    pid.PID_5_6   as sfx2-> nm.suffix = sfx2 "PID_5_6";
    pid.PID_5_5   as pfx -> nm.prefix = pfx "PID_5_5";
    pid.PID_5_7   as use -> nm.use    = translate(use, 'http://hl7.org/fhir/uv/v2mappings/ConceptMap/table-hl70200-to-name-use', 'code') "PID_5_7";
  } "name";

  // Geburtsdatum (PID-7)
  pid.PID_7 as dob -> patient.birthDate = dob "PID_7";

  // Geschlecht (PID-8.1)
  pid.PID_8_1 as sex
    -> patient.gender = translate(sex, 'http://hl7.org/fhir/uv/v2mappings/ConceptMap/table-hl70001-to-administrative-gender', 'code') "PID_8_1";
}

// ----- DG1 → Condition Fields -----
group DG1toConditionFields(source dg1 : DG1Segment, target condition : Condition, target patient : Patient) {
  // Code (DG1-3)
  dg1 -> condition.code as cc then {
    dg1 -> cc.coding as coding, coding.system as system, coding.code as code, coding.display as display, coding.version as version then {
      dg1.DG1_3_3 as vsys -> system.value   = vsys "DG1_3_3";
      dg1.DG1_3_1 as vcode -> code.value    = vcode "DG1_3_1";
      dg1.DG1_3_2 as vdisp -> display.value = vdisp "DG1_3_2";
      dg1.DG1_3_7 as vver  -> version.value = vver  "DG1_3_7";
    } "coding";
    dg1.DG1_3_9 as vtext -> cc.text as text, text.value = vtext "DG1_3_9";
  } "code";

  // Subject = Patient
  dg1 -> condition.subject = create('Reference') as ref,
        ref.reference = ('urn:uuid:' + %patient.id) "subject";
}

// ----- ServiceRequest Fields -----
group ServiceRequestFields(source src : InfoWashSource, target sr : ServiceRequest, target patient : Patient, target condition : Condition) {
  // Subject = Patient
  src -> sr.subject = create('Reference') as refSubj,
       refSubj.reference = ('urn:uuid:' + %patient.id) "refSubj";

  // Reason = Condition
  src -> sr.reasonReference = create('Reference') as refReason,
       refReason.reference = ('urn:uuid:' + %condition.id) "refReason";

  // Untergruppen laufen nur, wenn das jeweilige Segment existiert
  src.ORCSegment as orc then ORCtoServiceRequest(orc, sr) "orc";
  src.OBRSegment as obr then OBRtoServiceRequest(obr, sr) "obr";
  src.TQ1Segment as tq1 then TQ1toServiceRequest(tq1, sr) "tq1";
}

// ----- Provenance Fields -----
group ProvenanceFields(source src : InfoWashSource, target prov : Provenance, target patient : Patient, target condition : Condition, target sr : ServiceRequest) {
  // Target = All resources (Patient, Condition, ServiceRequest)
  src.CTXSegment as ctx -> prov.target = create('Reference') as ref1,
        ref1.reference = ('urn:uuid:' + %patient.id) "targetPatient";
  
  src.CTXSegment as ctx -> prov.target = create('Reference') as ref2,
        ref2.reference = ('urn:uuid:' + %condition.id) "targetCondition";
  
  src.CTXSegment as ctx -> prov.target = create('Reference') as ref3,
        ref3.reference = ('urn:uuid:' + %sr.id) "targetServiceRequest";

  // Agent with device ID
  src.CTXSegment as ctx -> prov.agent as ag then {
    ctx.CTX_DEVICE_id as dev -> ag.who = create('Reference') as ref, ref.identifier = create('Identifier') as id, id.value = dev "deviceId";
  } "agentWithDevice";

  // Entity with raw message and role
  src.CTXSegment as ctx -> prov.entity as ent then {
    ctx.CTX_RAW_message as raw -> ent.what = create('Reference') as wref, wref.display = raw "rawMessage";
    ctx.CTX_role as role -> ent.role = role "role";
  } "entityWithRawMessage";

}

// ========================== Segment-spezifische Gruppen ==========================

// ----- ORC → ServiceRequest (Intent/Status/AuthoredOn) -----
group ORCtoServiceRequest(source orc : ORCSegment, target sr : ServiceRequest) {
  // Intent immer "order" (gemäß Vorgabe)
  orc -> sr.intent = 'order' "intent-order";

  // Status
  orc.ORC_5 as st -> sr.status = st "ORC_5";

  // AuthoredOn nur wenn ORC_1 == "NW"
  orc.ORC_9 as dt where (%orc.ORC_1 = 'NW') -> sr.authoredOn = dt "ORC_9_when_ORC_1_NW";
}

// ----- OBR → ServiceRequest (Codes, Reason, Priority) -----
group OBRtoServiceRequest(source obr : OBRSegment, target sr : ServiceRequest) {
  // ServiceRequest.code (OBR-4)
  obr -> sr.code as cc then {
    obr -> cc.coding as coding, coding.code as code, coding.display as display, coding.system as system, coding.version as version then {
      obr.OBR_4_1 as v1 -> code.value    = v1 "OBR_4_1";
      obr.OBR_4_2 as v2 -> display.value = v2 "OBR_4_2";
      obr.OBR_4_3 as v3 -> system.value  = v3 "OBR_4_3";
      obr.OBR_4_7 as v7 -> version.value = v7 "OBR_4_7";
    } "coding";
    obr.OBR_4_9 as v9 -> cc.text as text, text.value = v9 "OBR_4_9";
  } "code";

  // Priority (OBR-5.1)
  obr.OBR_5_1 as pr -> sr.priority as p then {
    obr -> p.value = translate(pr, "http://hsrt-kkrt.org/fhir/ConceptMap/table-hl70485-to-request-priority", "code") "code";
  } "OBR_5_1";

  // reasonCode (OBR-31)
  obr -> sr.reasonCode as rc then {
    obr -> rc.coding as coding, coding.code as code, coding.display as display, coding.system as system, coding.version as version then {
      obr.OBR_31_1 as r1 -> code.value    = r1 "OBR_31_1";
      obr.OBR_31_2 as r2 -> display.value = r2 "OBR_31_2";
      obr.OBR_31_3 as r3 -> system.value  = r3 "OBR_31_3";
      obr.OBR_31_7 as r7 -> version.value = r7 "OBR_31_7";
    } "coding";
    obr.OBR_31_9 as r9 -> rc.text as rtext, rtext.value = r9 "OBR_31_9";
  } "reasonCode";
}

// ----- TQ1 → ServiceRequest (Occurrence boundsPeriod) -----
group TQ1toServiceRequest(source tq1 : TQ1Segment, target sr : ServiceRequest) {
  tq1 -> sr.occurrencePeriod as op then {
  tq1.TQ1_7 as TQ1_7 -> op.start = TQ1_7;
  tq1.TQ1_8 as TQ1_8 -> op.end = TQ1_8;
  } "timing";
}